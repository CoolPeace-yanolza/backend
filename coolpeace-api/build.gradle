bootJar.enabled = true

ext {
    JJWT_VERSION = "0.12.3"
    RESTDOCS_API_SPEC_VERSION = "0.18.2"
}

dependencies {
    implementation project(':coolpeace-core')
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'

    implementation "io.jsonwebtoken:jjwt-api:${JJWT_VERSION}"
    runtimeOnly "io.jsonwebtoken:jjwt-jackson:${JJWT_VERSION}"
    runtimeOnly "io.jsonwebtoken:jjwt-impl:${JJWT_VERSION}"

    // RestDocs
    testImplementation "org.springframework.restdocs:spring-restdocs-mockmvc"
    testImplementation "com.epages:restdocs-api-spec-mockmvc:${RESTDOCS_API_SPEC_VERSION}"

    testImplementation 'org.springframework.security:spring-security-test'
}

// Swagger UI 설정
def openapi3Path = "build/api-spec/openapi3.yaml"

openapi3 {
    server = "http://localhost:8080"
    title = "CoolPeace API"
    description = "CoolPeace API Document"
    version = "0.0.1"
    format = "yaml"

}

tasks.register("copyToSwaggerUI") {
    dependsOn "openapi3"
    doLast {
        copy {
            from openapi3Path
            into "src/main/resources/static/swagger-ui/."
        }
    }
}

tasks.register("copyTextToSecurity") {
    dependsOn copyToSwaggerUI
    doLast {
        def openapi3File = file(openapi3Path)
        def securityFile = file("src/test/resources/security.yaml")
        openapi3File.append securityFile.text
    }
}

test.finalizedBy copyTextToSecurity
